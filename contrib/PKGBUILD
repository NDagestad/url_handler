# Maintainer: Nicolai Dagestad <aur@nicolai.dagestad.fr>
pkgname='url_handler-git'
pkgver=1.0.0.r7.7675418
pkgrel=1
arch=('x86_64' 'aarch64')
licence=('GPL3')
pkgdesc="A little go program to handle opening URI/URLs for you"
depends=('glibc')
makedepends=('go' 'git' 'scdoc')
optdepends=('wl-clipboard: wayland clipboard support'
            'xclip: Xorg clipboard support')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=('etc/xdg/url_handler/config.ini')
source=('git+https://git.sr.ht/~nicolai_dagestad/url_handler')
sha256sums=('SKIP')

pkgver() {
	cd "${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	export GOPATH="$srcdir"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw "

	go build -ldflags "-X main.AppName=url_handler \
		-X main.VersionNumber=$pkgver"

	scdoc < url_handler.1.scd | gzip > url_handler.1.gz
	scdoc < url_handler.5.scd | gzip > url_handler.5.gz
}

package() {
	export GOPATH="$srcdir"
	cd "$srcdir/${pkgname%-git}"
	install -Dm 755 url_handler -t "$pkgdir/usr/bin";
	install -Dm 755 config.example.ini "$pkgdir/etc/xdg/url_handler/config.ini"
	install -Dm 755 url_handler.1.gz -t "$pkgdir/usr/share/man/man1"
	install -Dm 755 url_handler.5.gz -t "$pkgdir/usr/share/man/man5"
	mkdir -p "$pkgdir/usr/share/url_handler/"
}
